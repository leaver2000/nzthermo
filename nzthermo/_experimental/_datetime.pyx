# cython: language_level=3
# cython: boundscheck=False
# cython: wraparound=False
# cython: nonecheck=False
# cython: cdivision=True

# pyright: reportGeneralTypeIssues=false, reportUnusedExpression=false, reportMissingImports=false
from cython.parallel cimport parallel, prange
import numpy as np
cimport numpy as np

np.import_array()


cdef double pe4dt(long year, long month, bint apply_corection = 0) noexcept nogil:
    """POLYNOMIAL EXPRESSIONS FOR DELTA T (Î”T)
    see: https://eclipse.gsfc.nasa.gov/SEcat5/deltatpoly.html
    """
    cdef double delta_t, u, y, t

    y = year + (month - 0.5) / 12

    if year < -500:
        u = (y - 1820) / 100
        delta_t = -20 + 32 * u**2
    elif year < 500:
        u = y / 100
        delta_t = (
            10583.6
            - 1014.41 * u
            + 33.78311 * u**2
            - 5.952053 * u**3
            - 0.1798452 * u**4
            + 0.022174192 * u**5
            + 0.0090316521 * u**6
        )
    elif year < 1600:
        u = (y - 1000) / 100
        delta_t = (
            1574.2
            - 556.01 * u
            + 71.23472 * u**2
            + 0.319781 * u**3
            - 0.8503463 * u**4
            - 0.005050998 * u**5
            + 0.0083572073 * u**6
        )
    elif year < 1700:
        t = y - 1600
        delta_t = 120 - 0.9808 * t - 0.01532 * t**2 + t**3 / 7129
    elif year < 1800:
        t = y - 1700
        delta_t = (
            8.83
            + 0.1603 * t
            - 0.0059285 * t**2
            + 0.00013336 * t**3
            - t**4 / 1174000
        )
    elif year < 1860:
        t = y - 1800
        delta_t = (
            13.72
            - 0.332447 * t
            + 0.0068612 * t**2
            + 0.0041116 * t**3
            - 0.00037436 * t**4
            + 0.0000121272 * t**5
            - 0.0000001699 * t**6
            + 0.000000000875 * t**7
        )
    elif year < 1900:
        t = y - 1860
        delta_t = (
            7.62
            + 0.5737 * t
            - 0.251754 * t**2
            + 0.01680668 * t**3
            - 0.0004473624 * t**4
            + t**5 / 233174
        )
    elif year < 1920:
        t = y - 1900
        delta_t = (
            -2.79
            + 1.494119 * t
            - 0.0598939 * t**2
            + 0.0061966 * t**3
            - 0.000197 * t**4
        )
    elif year < 1941:
        t = y - 1920
        delta_t = 21.20 + 0.84493 * t - 0.076100 * t**2 + 0.0020936 * t**3
    elif year < 1961:
        t = y - 1950
        delta_t = 29.07 + 0.407 * t - t**2 / 233 + t**3 / 2547
    elif year < 1986:
        t = y - 1975
        delta_t = 45.45 + 1.067 * t - t**2 / 260 - t**3 / 718
    elif year < 2005:
        t = y - 2000
        delta_t = (
            63.86
            + 0.3345 * t
            - 0.060374 * t**2
            + 0.0017275 * t**3
            + 0.000651814 * t**4
            + 0.00002373599 * t**5
        )
    elif year < 2050:
        t = y - 2000
        delta_t = 62.92 + 0.32217 * t + 0.005589 * t**2
    elif year < 2150:
        delta_t = -20 + 32 * ((y - 1820) / 100) ** 2 - 0.5628 * (2150 - y)
    else:
        u = (y - 1820) / 100
        delta_t = -20 + 32 * u**2

    if apply_corection:
        delta_t -= -0.000012932 * (y - 1955) ** 2

    return delta_t


cdef double[:] _delta_t(long[:] years, long[:] months):
    cdef size_t i, N
    cdef double[:] out

    N = years.size
    out = np.empty(N, dtype=np.float64)
    with nogil, parallel():
        for i in prange(N, schedule='dynamic'):
            out[i] = pe4dt(years[i], months[i])

    return out

def delta_t(np.ndarray dt):
    """
    >>> np.array(['1970-01-01T02:00:00'], dtype="datetime64[Y]").astype(np.int64) + 1970
    array([1970])
    >>> np.array(['1970-03-01T02:00:00'], dtype="datetime64[M]").astype(np.int64) % 12 + 1
    array([3])
    """
    cdef long[:] Y = dt.astype("datetime64[Y]").astype(np.int64) + 1970
    cdef long[:] M = dt.astype("datetime64[M]").astype(np.int64) % 12 + 1
    return np.array(_delta_t(Y, M), dtype=np.float64, copy=False)


